# nnet-backward.awk
# Calcola e salva i delta del layer di output
# Usa: activation_derivative da activation.awk
# Richiede: -f activation.awk -f math.awk
# Richiede: -v delta_output_file="path/output.txt"

BEGIN {
    FS = OFS = " "
    eta = (learning_rate == "") ? 0.1 : learning_rate
    row_output = 0
    row_target = 0
    if (debug == "") debug = 0

    if (delta_output_file == "") {
        print "Errore: manca delta_output_file" > "/dev/stderr"
        exit 2
    }

    # Apro file per scrittura
    output_fh = delta_output_file
    if ((out = system("rm -f \"" output_fh "\"")) != 0) {
        print "Errore: impossibile rimuovere file " output_fh > "/dev/stderr"
    }
}

# Carica target
FILENAME == target_file {
    target[row_target++] = $NF
    next
}

# Carica output
FILENAME == output_file {
    for (i = 1; i <= NF; i++) {
        output[row_output, i] = $i
    }
    row_output++
    next
}

END {
    if (row_output != row_target) {
        print "Errore: mismatch righe output-target" > "/dev/stderr"
        exit 1
    }

    for (r = 0; r < row_output; r++) {
        line = ""
        for (j = 1; (r, j) in output; j++) {
            y = output[r, j]
            t = target[r]
            delta = (y - t) * activation_derivative(y, activation_function)
            if (debug) {
                printf "# r=%d j=%d y=%.6f t=%.6f delta=%.6f\n", r, j, y, t, delta > "/dev/stderr"
            }
            line = line sprintf("%f ", delta)
        }
        # Scrive la riga nel file output
        print line > delta_output_file
    }

    close(delta_output_file)
}

