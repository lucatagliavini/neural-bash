# nnet-backward.awk
# Calcolo dei delta per l'output layer
# Richiede: -f lib/functions/activation.awk -f lib/functions/math.awk
# Parametri richiesti:
#   -v output_file=...     (es: tmp/layer2.out)
#   -v target_file=...     (es: dataset/and-target.txt)
#   -v activation_function (es: sigmoid)
#   -v learning_rate=...   (es: 0.1, default)

# nnet-backward.awk
# Calcolo dei delta per il layer di output.
# Funziona insieme a: activation.awk, math.awk
#
# Per il caricamento FILENAME, rispettare l'ordine dei file sia nelle variabili
# che nel input finale del comando. Altrimenti l'ordine dei dati potrebbe essere
# errato.
#

BEGIN {
    FS = OFS = " "
    eta = (learning_rate == "") ? 0.1 : learning_rate
    row_output = 0
    row_target = 0
    if (debug == "") debug = 0
}

# Carica target
FILENAME == target_file {
    target[row_target++] = $NF
    next
}

# Carica output
FILENAME == output_file {
    for (i = 1; i <= NF; i++) {
        output[row_output, i] = $i
    }
    row_output++
    next
}

END {
    # Validazione righe
    if (row_output != row_target) {
        print "Errore: numero di righe mismatch tra output e target" > "/dev/stderr"
        print "output rows =", row_output, " target rows =", row_target > "/dev/stderr"
        exit 1
    }

    for (r = 0; r < row_output; r++) {
        for (j = 1; (r, j) in output; j++) {
            y = output[r, j]
            t = target[r]
            delta = (y - t) * activation_derivative(y, activation_function)
            if (debug) {
                printf "# r=%d j=%d y=%.6f t=%.6f delta=%.6f\n", r, j, y, t, delta > "/dev/stderr"
            }
            printf "%f ", delta
        }
        printf "\n"
    }
}

